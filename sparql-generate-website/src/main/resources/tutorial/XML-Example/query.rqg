BASE <http://example.com/> 
PREFIX iter: <http://w3id.org/sparql-generate/iter/>
PREFIX fun: <http://w3id.org/sparql-generate/fn/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#> 
PREFIX mail: <http://example.com/mail#>
PREFIX note: <http://example.com/note/>
PREFIX ex: <http://example.com/>

GENERATE { 
    <http://example.com/note/{?noteId}> a mail:note;
      mail:from ?from;
      mail:to ?to;
      mail:heading ?heading;
      mail:body ?body ;
      mail:when <http://example.com/DateTime/{?year}/{?month}/{?day}> .

    <http://example.com/DateTime/{?year}/{?month}/{?day}> a ex:DateTime;
      ex:day "{?day}"^^xsd:gDay ;
      ex:month "{?month}"^^xsd:gMonth ;
      ex:year "{?year}"^^xsd:gYear .       
}
SOURCE <http://example.com/example.xml> AS ?source
ITERATOR iter:XPath(?source, "//note" ) AS ?note
WHERE {
     BIND( fun:XPath(?note,"/note/@id"  ) AS ?noteId )
     BIND( fun:XPath(?note,"/note/@day"  ) AS ?day )
     BIND( fun:XPath(?note,"/note/@month"  ) AS ?month )
     BIND( fun:XPath(?note,"/note/@year"  ) AS ?year )
     BIND( fun:XPath(?note,"/note/from/text()"  ) AS ?from )
     BIND( fun:XPath(?note,"/note/to/text()"  ) AS ?to )
     BIND( fun:XPath(?note,"/note/heading/text()"  ) AS ?heading )
     BIND( fun:XPath(?note,"/note/body/text()"  ) AS ?body )
} 