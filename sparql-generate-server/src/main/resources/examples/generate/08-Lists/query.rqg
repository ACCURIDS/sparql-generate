BASE <https://ci.mines-stetienne.fr/aqi/data/>
PREFIX fun: <http://w3id.org/sparql-generate/fn/>
PREFIX iter: <http://w3id.org/sparql-generate/iter/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX sosa: <http://www.w3.org/ns/sosa/>
PREFIX aqio: <https://ci.mines-stetienne.fr/aqi/ontology#>
PREFIX ex: <http://example.org/>

GENERATE {
<loc/{?idx}> a sosa:FeatureOfInterest;
    rdfs:label ?name .
  
  ?observation a aqio:AirQualityIndexObservation;
    sosa:hasSimpleResult ?aqi ;
    sosa:hasFeatureOfInterest <loc/{?idx}>.
      

  <loc/world> ex:observationList LIST( ?observation ) .

}
ITERATOR iter:for(0,1,9) AS ?index
SOURCE <https://ci.mines-stetienne.fr/aqi/static/station/{STR(?index)}> AS ?source
WHERE {
  BIND(STR(xsd:integer(fun:JSONPath(?source,"$.data.idx"))) AS ?idx)
  BIND(fun:JSONPath(?source,"$.data.city.name") AS ?name)
  BIND(xsd:integer(fun:JSONPath(?source,"$.data.aqi")) AS ?aqi)
  BIND( BNODE() AS ?observation )
  FILTER( BOUND( ?aqi ) )
} ORDER BY DESC (?aqi)