BASE <http://example.com/> 
PREFIX ite: <http://w3id.org/sparql-generate/iter/>
PREFIX transit: <http://vocab.org/transit/terms/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#> 
PREFIX ex: <http://example.com/ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
GENERATE { 
    <http://airport.example.com/{ ?id }> a transit:Stop;
          ex:latlong <http://loc.example.com/latlong/{?lat},{?long}> .

    <http://loc.example.com/latlong/{?lat},{?long}> ex:lat "{?lat}"^^xsd:decimal;
                ex:long "{?long}"^^xsd:decimal .
  
    GENERATE {
      <http://airport.example.com/{ ?id }> ex:route <http://trans.example.com/bus/{ ?busId }> .
      <http://trans.example.com/bus/{ ?busId }> a ex:Transport ;
         ex:type <http://trans.example.com/vehicle/{ ?type }> .

      GENERATE {
          <http://trans.example.com/bus/{ ?busId }> ex:stop <http://trans.example.com/stop/{?busStopId}> .
          <http://trans.example.com/stop/{?busStopId}> rdfs:label ?busStopLabel;
                      ex:id "{?busStopId}"^^xsd:integer .
      } ITERATOR ite:XPath(?sourcetransport, "//bus[@id={ ?busId }]//stop", "/stop/@id", "/stop/text()" ) AS ?busStop ?busStopId ?busStopLabel ?position.
    } ITERATOR ite:XPath( <http://example.com/transport.xml> , "/", "//bus[@id={ ?stop }]/@id", "//bus[@id={ ?stop }]/@type")  AS ?sourcetransport ?busId ?type ?position.

    GENERATE {
      <http://airport.example.com/{ ?id }> ex:location ?cityURI .
      <http://loc.example.com/city/{ ?cityName}> a ex:City;
               ex:countryCode ?countryCode;
               ex:onContinent ?continentCode .
    }
    ITERATOR ite:JSONPath(<http://example.com/venue.json>, "$..venue[?(@.location.city=='{ ?city }')]", "$.location.city", "$.location.country", "$.location.continent"  ) AS ?cityObject ?cityName  ?countryCode  ?continentCode .
  
} ITERATOR ite:CSV(<http://example.com/airport.csv>, "id", "city", "stop", "longitude", "latitude" ) AS ?id ?city ?stop ?long ?lat
