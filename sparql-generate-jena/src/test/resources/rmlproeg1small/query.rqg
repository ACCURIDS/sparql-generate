BASE <http://example.com/>  
PREFIX ite: <http://w3id.org/sparql-generate/iter/>
PREFIX fun: <http://w3id.org/sparql-generate/fn/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#> 
PREFIX mail: <http://example.com/mail#>
PREFIX note: <http://example.com/note/>
PREFIX ex: <http://example.com/>

GENERATE { 
    <http://example.com/note/{ fun:XPath(?note,"/note/@id") }>
            a mail:note;
            mail:from ?{ fun:XPath(?note,"/note/from/text()") };
            mail:to ?{ fun:XPath(?note,"/note/to/text()"  ) };
            mail:heading ?{ fun:XPath(?note,"/note/heading/text()"  ) };
            mail:body ?{ fun:XPath(?note,"/note/body/text()"  ) } ;
            mail:when <http://example.com/DateTime/{ ?year }/{ ?month }/{ ?day }> .

    <http://example.com/DateTime/{ ?year }/{ ?month }/{ ?day }>
            a ex:DateTime;
            ex:day "{ ?day }"^^xsd:gDay ;
            ex:month "{ ?month }"^^xsd:gMonth ;
            ex:year "{ ?year }"^^xsd:gYear .  

}
SOURCE <http://example.com/example1.xml> AS ?source
ITERATOR ite:XPath(?source, "//note" ) AS ?note
WHERE {
     BIND( fun:XPath(?note,"/note/@day" ) AS ?day )
     BIND( fun:XPath(?note,"/note/@month" ) AS ?month )
     BIND( fun:XPath(?note,"/note/@year" ) AS ?year )
}