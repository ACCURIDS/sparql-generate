# Copyright 2020 MINES Saint-Ã‰tienne
# 
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
# 
#      http://www.apache.org/licenses/LICENSE-2.0
# 
# Unless required by applicable law or agreed to in writing, 
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

BASE <http://example.com/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX seas: <https://w3id.org/seas/ontology/all#>
PREFIX rqg-ite: <http://w3id.org/sparql-generate/iter/>
PREFIX rqg-fun: <http://w3id.org/sparql-generate/fn/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX dc: <http://purl.org/dc/elements/1.1/>

GENERATE {

    <> a foaf:Document ;
        rdfs:comment """This document demonstrates the generation of RDF from the CNR charging plan. The schema used by this RDF is still to be validated !"""@en ;
        foaf:primaryTopic <Execution> .

    <execution> a seas:AlgorithmExecution .


    GENERATE {

        GENERATE {
            
            [] a seas:Evaluation ;
                seas:SystemOfInterest ?charge ;
                seas:temporalContext [ seas:start ?start ; seas:end ?end ] ;
                seas:constantPower ?power .


        } ITERATOR rqg-ite:XPath(?chargingPlan, "//Order") AS ?order
        WHERE {
            BIND( strdt( rqg-fun:XPath(?order, "//DateDebut/text()"), xsd:dateTime ) AS ?start )
            BIND( strdt( rqg-fun:XPath(?order, "//DateFin/text()"), xsd:dateTime ) AS ?end )
            BIND( xsd:decimal( rqg-fun:XPath(?order, "//Puissance/text()") ) AS ?power )
        
        } .

    } ITERATOR rqg-ite:XPath(?message, "//ChargingPlan") AS ?chargingPlan
    WHERE  {
        BIND( rqg-fun:XPath(?chargingPlan, "//IdCharge/text()") AS ?idCharge )
        BIND( IRI( CONCAT ("Charge/" , ?idCharge ) ) AS ?charge )
    } .


} 
SOURCE <http://cnr-seas.cloudapp.net/scp/rest/ChargingPlan/1fc4ltccsrsun9mdijrgaruhe5> AS ?message
	
