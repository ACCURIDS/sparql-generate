BASE <http://example.com/> 
PREFIX ite: <http://w3id.org/sparql-generate/iter/>
PREFIX fun: <http://w3id.org/sparql-generate/fn/>
PREFIX schema: <http://schema.org/>
PREFIX wgs84_pos: <http://www.w3.org/2003/01/geo/wgs84_pos#>
PREFIX gn: <http://www.geonames.org/ontology#>
PREFIX transit: <http://vocab.org/transit/terms/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX geosp: <http://www.telegraphis.net/ontology/geography/geography#>

GENERATE { 
    <http://airport.example.com/{?id}> a transit:Stop;
          wgs84_pos:location <http://loc.example.com/latlong/{?lat},{?long}>;
          wgs84_pos:location <http://loc.example.com/city/{?cityName}> ;
          transit:service <http://trans.example.com/busses/{ ?busId }> .

    <http://loc.example.com/city/{?cityName}> a schema:City ;
             gn:countryCode ?countryCode;
             geosp:onContinent ?continentCode;
             wgs84_pos:location <http://loc.example.com/latlong/{?lat},{?long}> .

    <http://trans.example.com/busses/{ ?busId }>  a transit:Stop;
             transit:stop <http://trans.example.com/stop/{ ?busStopId }> .

    <http://trans.example.com/stop/{ ?busStopId }> rdfs:label ?busStopLabel .
  
    <http://loc.example.com/latlong/{?lat},{?long}> wgs84_pos:lat ?lat;
                wgs84_pos:long ?long .

    <http://loc.example.com/latlong/{?vlatitude},{?vlongitude}> wgs84_pos:lat ?vlatitude;
            wgs84_pos:long ?vlongitude .
}
SOURCE <http://example.com/venue.json> AS ?sourcevenue 
ITERATOR ite:CSV(<http://example.com/airport.csv> , "id", "city", "bus", "latitude", "longitude" ) AS ?id ?city ?bus ?lat ?long
ITERATOR ite:XPath(<http://example.com/transport.xml>, "/transport/bus" , "/bus/@id" ) AS ?bus ?busId
ITERATOR ite:XPath(?bus, "/bus/route" ) AS ?busRoute
ITERATOR ite:XPath(?busRoute, "/route/stop" , "/stop/@id" , "/stop/text()" ) AS ?busStop ?busStopId ?busStopLabel
WHERE {
    BIND(fun:JSONPath(?sourcevenue, "$.location.city" ) AS ?cityName)
    BIND(fun:JSONPath(?sourcevenue, "$.location.country" ) AS ?countryCode)
    BIND(fun:JSONPath(?sourcevenue, "$.location.continent" ) AS ?continentCode)
    BIND(fun:JSONPath(?sourcevenue, "$.venue.latitude" ) AS ?vlatitude)
    BIND(fun:JSONPath(?sourcevenue, "$.venue.longitude" ) AS ?vlongitude)
}