BASE <http://example.com/> 
PREFIX ite: <http://w3id.org/sparql-generate/iter/>
PREFIX fun: <http://w3id.org/sparql-generate/fn/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#> 
PREFIX ex: <http://example.com/> 

GENERATE { 
  [] ex:countryName ?countryName ;
     ex:countryCode ?countryCode ;
     ex:continentCode ?continentCode;
     ex:continentName ?continentName;
     ex:countryPopulation ?countryPopulation;
}
SOURCE <http://example.com/continent.json> AS ?continents
SOURCE <http://example.com/country-names.json> AS ?countryNames
SOURCE <http://example.com/country-population.json> AS ?countriesPopulation
SOURCE <http://example.com/continentnames.json> AS ?continentNames

ITERATOR ite:JSONListKeys( ?countryNames ) AS ?countryCode
WHERE {
    BIND( fun:JSONPath( ?countryNames, CONCAT("$.",?countryCode) ) AS ?countryName )
    BIND( fun:JSONPath( ?continents, CONCAT("$.",?countryCode) ) AS ?continentCode)
    BIND( fun:JSONPath( ?continentNames, CONCAT("$.",?continentCode) ) AS ?continentName)
    BIND (CONCAT("$[?(@.country=='",?countryName,"')].population") AS ?populationQuery)
    BIND( xsd:decimal(fun:JSONPath( ?countriesPopulation, ?populationQuery )) AS ?countryPopulation )
} 
ORDER BY DESC(?countryPopulation) 
LIMIT 10

