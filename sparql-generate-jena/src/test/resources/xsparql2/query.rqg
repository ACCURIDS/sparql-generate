BASE <http://example.com/> 
PREFIX iter: <http://w3id.org/sparql-generate/iter/>
PREFIX fn: <http://w3id.org/sparql-generate/fn/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
GENERATE { 
  ?personNode a foaf:Person .
  ?personNode foaf:name ?personName .
    GENERATE {
        ?personNode foaf:knows ?knowsPersonNode .
        ?knowsPersonNode a foaf:Person .
        ?knowsPersonNode foaf:name ?knowsPerson .
    }
    ITERATOR iter:XPath(?person, "//knows" ) AS ?knows 
    WHERE {
        BIND( fn:XPath(?knows,"/knows/text()"  ) AS ?knowsPerson )
        BIND (BNODE(?knowsPerson) AS ?knowsPersonNode)
    } .
}
SOURCE <http://example.com/relations.xml> AS ?source
ITERATOR iter:XPath(?source, "//person" ) AS ?person
WHERE {
    BIND( fn:XPath(?person,"/person/@name"  ) AS ?personName )
    BIND (BNODE(?personName) AS ?personNode)
}