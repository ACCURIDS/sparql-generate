BASE <http://example.com/>
PREFIX rqgite: <http://w3id.org/sparql-generate/iter/>
PREFIX rqgfun: <http://w3id.org/sparql-generate/fn/>
PREFIX seas: <https://w3id.org/seas/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>


GENERATE{
    GENERATE {
         [] a seas:Evaluation;
              seas:generatedAt   ?horodate;
              seas:generatedBy   ?ActivityURI;
              seas:quantity      [a seas:ElectricConsumption];
              seas:unit          [a seas:KiloWatt];
              seas:systemOfInterest ?smartMeterURI;
             seas:timeSeries  ?timeSeriesURI.
        ?timeSeriesURI a seas:TimeSeries;
              seas:timeStep "PT1M"^^xsd:duration;
              seas:start ?firstTimeDT;
              seas:list ?consumptionTimeSeriesListURI.
        ?ActivityURI a seas:ElectricConsumptionMeasuringActivity.

        GENERATE{
            ?this1 a rdf:List;rdf:first  ?value ;rdf:rest ?rest1 .
        }
        ITERATOR rqgite:JSONElement(?dataSensor,"$.PXm[*]") AS ?key
        WHERE{
            BIND(xsd:decimal(rqgfun:JSONPath(?key, "$.position" )) AS ?position)
            BIND(xsd:boolean(rqgfun:JSONPath(?key, "$.hasNext" )) AS ?hasNext)
            BIND( xsd:double(rqgfun:JSONPath(?key, "$.element[1]")) AS ?value)

            BIND( IF( 0 = ?position , ?consumptionTimeSeriesListURI  ,
             IRI( CONCAT( "http://example.com/smartmeter/",?id, "/activity/timeseries/", STR(?firstTime),"/listvalue/",
             "e_" , STR (?position) ) ) ) AS ?this1 )
             BIND( STR( ?position + 1) as ?nextPosition)
             BIND( IF( true = ?hasNext , IRI( CONCAT("http://example.com/smartmeter/",?id, "/activity/timeseries/",STR(?firstTime),"/listvalue/",
             "e_" , ?nextPosition) ) , rdf:nil ) AS ?rest1 )

        }.
    }
    WHERE {
       BIND(rqgfun:JSONPath(?source, "$.sensorId" ) AS ?id)
       BIND(rqgfun:JSONPath(?source, "$.dataSensor.Version" ) AS ?version)
       BIND(  rqgfun:dateTime( rqgfun:JSONPath(?source, "$.horodate" ) ) AS ?horodate)
       BIND (URI(CONCAT("http://example.com/smartmeter/",?id)) AS ?smartMeterURI)
       BIND (URI(CONCAT("http://example.com/smartmeter/",?id, "/activity")) AS ?ActivityURI)
       BIND (URI(CONCAT("http://example.com/smartmeter/",?id,"/timeseries")) AS ?timeSeriesURI)
       BIND ( rqgfun:JSONPath( ?dataSensor , "$.PXm[0]" ) AS ?msgtime)
       BIND ( rqgfun:JSONPath( ?msgtime, "$[0]" ) AS ?firstTime)
       BIND ( rqgfun:dateTime( ?firstTime ) AS ?firstTimeDT)
       BIND (URI(CONCAT("http://example.com/smartmeter/", ?id, "/activity/timeseries/", STR( ?firstTime ) , "/listvalue/") ) AS ?consumptionTimeSeriesListURI )
    }.
}
SOURCE <http://example.com/smartmeters/elecW6labs.json> AS ?msg
ITERATOR rqgite:JSONPath( ?msg , "$.[*]" ) AS ?source
ITERATOR rqgite:JSONPath( ?msg , "$.[*].dataSensor.Starts[*]" ) AS ?dataSensor